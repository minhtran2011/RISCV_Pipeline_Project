module control_unit (
    input wire [6:0] opcode,
    output reg branch,
    output reg mem_read,
    output reg mem_to_reg,
    output reg [1:0] alu_op,
    output reg mem_write,
    output reg alu_src,
    output reg reg_write
);
    always @(*) begin
        // Trạng thái mặc định để tránh sinh ra Latch không mong muốn
        branch     = 1'b0; mem_read   = 1'b0; mem_to_reg = 1'b0;
        alu_op     = 2'b00; mem_write  = 1'b0; alu_src    = 1'b0;
        reg_write  = 1'b0;

        case (opcode)
            7'b0110011: begin // R-type (add, sub, and, or...)
                reg_write = 1'b1; alu_op = 2'b10;
            end
            7'b0010011: begin // I-type (addi...)
                alu_src = 1'b1; reg_write = 1'b1; alu_op = 2'b00;
            end
            7'b0000011: begin // Load (lw)
                alu_src = 1'b1; mem_to_reg = 1'b1; reg_write = 1'b1; mem_read = 1'b1; alu_op = 2'b00;
            end
            7'b0100011: begin // Store (sw)
                alu_src = 1'b1; mem_write = 1'b1; alu_op = 2'b00;
            end
            7'b1100011: begin // Branch (beq)
                branch = 1'b1; alu_op = 2'b01;
            end
        endcase
    end
endmodule